buildscript {
    repositories {
        mavenCentral()
        maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.0"
    }
}

apply plugin: 'java-library'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'io.codearte.nexus-staging'
apply from: 'publish.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def projectName = 'jdkgdxds'
version '0.0.4-SNAPSHOT'
group 'com.github.tommyettinger'

//api "com.github.tommyettinger:jdkgdxds:0.0.3"

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

// Disable JDK 8's doclint
// http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            // The -quiet is because of some sort of weird JDK JavaCompiler bug:
            // https://discuss.gradle.org/t/passing-arguments-to-compiler-and-javadoc/1661
            options.addStringOption('Xdoclint:none,-missing', '-quiet')
            options.tags('apiNote:a:"API Note"', 'implSpec:a:"Implementation Requirements"', 'implNote:a:"Implementation Note"')
        }
    }
}

compileJava.exclude("emu")
javadoc.exclude("emu")
javadoc.options.source("15")
javadoc.destinationDir = file("docs/apidocs")

sourceSets.test.resources.srcDirs += [ file('src/test/resources').path ]

jar {
    archiveBaseName.set(projectName)
    manifest {
        attributes 'Implementation-Title': projectName, 'Implementation-Version': archiveVersion
    }
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
    api 'com.google.code.findbugs:jsr305:3.0.2'
    testImplementation "junit:junit:4.13.1"
}
